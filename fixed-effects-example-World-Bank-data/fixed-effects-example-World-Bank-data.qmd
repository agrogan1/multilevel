<<dd_version: 2>>
<<dd_include: header.txt >>


Comparing Fixed Effects and Multilevel Models Using World Bank Data
===============================================================

# Background

Multilevel models for longitudinal data, and fixed effects regression provide two alternative methods for analyzing longitudinal data.

Briefly…

> Here, level 2 units are countries. Level 1 is a country-year. Level 2 is the country.

> * Multilevel models use both within level 2 unit and between level 2 unit variation, and provide statistical control for observed variables that are included in the model.
>
> * Fixed effect regressions use only within level 2 unit variation. As a consequence, fixed effects regression is unable to provide parameter estimates for time invariant variables, even when they are included in the statistical model. Fixed effects regressions provide statistical controls for all time invariant variables, whether observed or unobserved.

# Equation

Both models estimate the following equation.

$$y_{it} = \beta_0 + \beta_1 x_{it} + u_{0i} + e_{it}$$

Here $\beta_0$ is the intercept, $\beta_1$ is a slope, $u_{0i}$ is a country specific intercept, and $e_{it}$ is a measurement specific error term.

> In the *multilevel model*, the $u_{0i}$ are considered to have a *distribution*, with a mean of 0 and a standard deviation $\sigma_{u0}$. In the *fixed effects regression model*, the $u_{0i}$ are considered to be *fixed*, and *directly estimable*, although in practice, estimates for each of the $u_{0i}$ are usually not provided.

# Get The Data

We are going to data from the [World Bank World Development Indicators](https://agrogan1.github.io/R/wbdemo/)

~~~~
<<dd_do>>
use "WorldBankData.dta", clear

describe life_expectancy year Gini GDP undernourishment region
<</dd_do>>
~~~~

# Process The Data

~~~~
<<dd_do>>
drop if region == "Aggregates" // drop rows of data representing Aggregates

encode region, generate(regionNUMERIC) // numeric version of region
<</dd_do>>
~~~~

# Graph

~~~~
<<dd_do>>
histogram life_expectancy, scheme(michigan) title("Life Expectancy at Birth") fcolor(%50)
<</dd_do>>
~~~~

<<dd_graph: sav("histogram.svg") alt("scatter mpg weight") replace height(400)>>

# Multilevel Model (`mixed y x || id:`)

> The model uses within and between level 2 unit variation. Estimates are provided for all variables. The model only controls for variables that are included in the model.

~~~~
<<dd_do>>
mixed life_expectancy year Gini GDP undernourishment i.regionNUMERIC || country:

est store MLM
<</dd_do>>
~~~~

# Fixed Effects Model (`xtreg y x, i(id) fe`)

> The model uses only within level 2 unit variation. Estimates are only provided for within level 2 unit change over time. The model controls for all time invariant variables whether observed or unobserved.

~~~~
<<dd_do>>
xtreg life_expectancy year Gini GDP undernourishment i.regionNUMERIC, i(country) fe

est store FE
<</dd_do>>
~~~~

# Compare The Two Sets of Estimates (`estimates table`)

> NB that the omitted category for region is "East Asia & Pacific".

> 1. The multilevel model controls for variables that are included in the model.
>
> 2. The fixed effects model controls for variables that are included in the model, as well as all time invariant characteristics of countries.
>
> 3. The multilevel model uses both within and between country variation; the fixed effects model uses only within country variation.
>
> 4. The fixed effects model is unable to provide information on time invariant characteristics of countries even if they are included in the model.
>
> 5. Coefficients in the fixed effects model are generally smaller than coefficients in the multilevel model. (Often, though not in this example, coefficients that were significant in the multilevel model are not significant in the fixed effects model).

~~~~
<<dd_do>>
est table MLM FE, star equations(1)  b(%9.3f) stats(N r2_a)
<</dd_do>>
~~~~



