---
title: "Comparing Multilevel Models and Fixed Effects Regression"
author: "Andrew Grogan-Kaylor"
date: "today"
format:
  html:
    toc: true
    number-sections: true
    theme: yeti
    lightbox: true
---

# Background 

```{r}
#| output: false
#| echo: false

library(Statamarkdown)

```

::: {.callout-note}
This example draws from the Stata documentation for the `xtreg` command.
:::

Multilevel models for longitudinal data, and fixed effects regression provide two alternative methods for analyzing longitudinal data.

Brieflyâ€¦

* Multilevel models use both within person and between person variation, and provide statistical control for observed variables that are included in the model.
* Fixed effect regressions use only within person variation. As a consequence, fixed effects regression is unable to provide parameter estimates for time invariant variables, even when they are included in the statistical model. Fixed effects regressions provide statistical controls for all time invariant variables, whether observed or unobserved.

# Get The Data (`use`)

We are going to use the sample NLS data on work from *Stata Corporation*.

```{stata, collectcode=TRUE}

clear all

use https://www.stata-press.com/data/r16/nlswork, clear

```

# Describe the Key Variables (`describe`)

```{stata}

describe ln_w grade age race union south

```

# Equation

Both models estimate the following equation.

$$y_{it} = \beta_0 + \beta_1 x_{it} + u_{0i} + e_{it}$$

Here $\beta_0$ is the intercept, $\beta_1$ is a slope, $u_{0i}$ is a person specific intercept, and $e_{it}$ is a measurement specific error term.

::: {.callout-tip}
## Key Idea

In the *multilevel model* discussed below, the $u_{0i}$ are considered to have a *distribution*, with a mean of 0 and a standard deviation $\sigma_{u0}$. In the *fixed effects regression model*, the $u_{0i}$ are considered to be *fixed*, and *directly estimable*, although in practice, estimates for each of the $u_{0i}$ are usually not provided.
:::

# Multilevel Model (`mixed y x || id:`)

::: {.callout-tip}
## MLM

The model uses within and between person variation. Estimates are provided for all variables. The model only controls for variables that are included in the model.
:::

```{stata, collectcode=TRUE}

mixed ln_w grade age i.race union south || idcode: 
	
est store MLM

```

# Fixed Effects Regression (`xtreg y x, i(id) fe`)

::: {.callout-tip}
# FE

The model uses only within person variation. Estimates are only provided for within person change over time. The model controls for all time invariant variables whether observed or unobserved.
:::

```{stata, collectcode=TRUE}

xtreg ln_w grade age i.race union south, i(idcode) fe
	
est store FE

```

# Compare The Two Sets of Estimates (`estimates table`)

1. The multilevel model controls for variables that are included in the model.
2. The fixed effects model controls for variables that are included in the model, as well as all time invariant characteristics of individuals.
3. The multilevel model uses both within and between individual variation; the fixed effects model uses only within individual variation.
4. The fixed effects model is unable to provide information on time invariant characteristics of individuals (AKA Level 2 variables) even if they are included in the model. All time invariant characteristics of individuals (AKA Level 2 variables) are excluded from the estimates.
5. As a result of (3), the fixed effects model is less statistically efficient than the multilevel model. Coefficients in the fixed effects model are generally smaller than coefficients in the multilevel model. (Often, though not in this example, coefficients that were significant in the multilevel model are not significant in the fixed effects model). Sometimes this is a result of the increased statistical control afforded by the fixed effects regression; sometimes this is a result of the decreased statistical efficiency of the fixed effects regression.
6. Multilevel models rely on the assumption that the random effects ($u$'s) are uncorrelated with the regressors ($x$'s). Fixed effects regression models do not rely on this assumption. 
7. Random slopes are a part of the multilevel modeling framework allowing for exploration of the diversity of regression slopes across individuals and contexts. Random slopes are not part of the fixed effects regression framework.

```{stata}

etable, estimates(MLM FE) column(estimate) showstars showstarsnote

```





